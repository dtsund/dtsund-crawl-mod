# Corridors, turns etc, with statue and fountain arrangements.
#
# Place a moderate amount of vaults. Having too many of the vaults seems
# a bit messy, considering how randomly they are placed, and how the
# statues stand out (especially in tiles).
NAME:   serial_corexii_hall
TAGS:   allow_dup extra luniq_serial
#       Fits best in the rooms layout.
TAGS:   layout_rooms
DEPTH:  D, Elf, Crypt, Snake
CHANCE: 1%
{{
  local c = 2 + crawl.random2(3)*crawl.random2(3)
  hook("post_place", function()
    dgn.place_maps{tag="serial_corexii_hall", count=c}
  end)
}}

# 3-wide hallways lined with statues, by Core Xii.
NAME:   corexii_hall_statues_deadend
DEPTH:  D, Elf, Crypt, Snake
WEIGHT: 1
TAGS:   extra allow_dup serial_corexii_hall
SUBST:  T = T:2 V:2 G:1
NSUBST: ? = 1:% / *:.
MAP
xxxxxxxxxxxxx
xGxxxGxxxGxxx
@.......?.?xx
@..........Tx
@.......?.?xx
xGxxxGxxxGxxx
xxxxxxxxxxxxx
ENDMAP

NAME:   corexii_hall_statues_i
DEPTH:  D, Elf, Crypt, Snake
WEIGHT: 2
TAGS:   extra allow_dup serial_corexii_hall
SUBST:  T = T:2 V:2 G:1
SUBST:  T = T:1 G:4, T = T V
MAP
xxxxxxxxxxx
xGxxxTxxxGx
@.........@
@.........@
@.........@
xGxxxTxxxGx
xxxxxxxxxxx
ENDMAP

NAME:   corexii_hall_statues_l
DEPTH:  D, Elf, Crypt, Snake
WEIGHT: 2
TAGS:   extra allow_dup serial_corexii_hall
SUBST:  T = T:1 G:2, T = T V
MAP
xx@@@xx
xG...Gx
xx...xx
xx...xx
xx...xx
xT...Gx
xx...xxxxxxxx
xx...xxGxxxGx
xx..........@
xG..........@
xx..........@
xxxGxxxTxxxGx
xxxxxxxxxxxxx
ENDMAP

NAME:   corexii_hall_statues_s
DEPTH:  D, Elf, Crypt, Snake
WEIGHT: 1
TAGS:   extra allow_dup serial_corexii_hall
SUBST:  T = T:4 G:1, T = T V
MAP
xx@@@xx
xG...Gx
xx...xxxxxxxxxx
xx...xxTxxxGxxx
xx...........xx
xG...........Gx
xx...........xx
xxxGxxxTxx...xx
xxxxxxxxxx...xx
        xG...Gx
        xx@@@xx
ENDMAP

NAME:   corexii_hall_statues_u
DEPTH:  D, Elf, Crypt, Snake
WEIGHT: 1
TAGS:   extra allow_dup serial_corexii_hall
SUBST:  T = T:4 G:1, T = T V
MAP
xx@@@xxxxx@@@xx
xG...GxxxG...Gx
xx...xxxxx...xx
xx...xxTxx...xx
xx...........xx
xG...........Gx
xx...........xx
xxxGxxxTxxxGxxx
xxxxxxxxxxxxxxx
ENDMAP

NAME:   corexii_hall_statues_t
DEPTH:  D, Elf, Crypt, Snake
WEIGHT: 1
TAGS:   extra allow_dup serial_corexii_hall
SUBST:  T = T:4 G:1, T = T V
SUBST:  T = T:2 V:2 G:1
MAP
xxxxxxxxxxxxxxxxxxx
xGxxxGxxxTxxxGxxxGx
@.................@
@.................@
@.................@
xGxxxGxx...xxGxxxGx
xxxxxxxx...xxxxxxxx
      xG...Gx
      xx...xx
      xx...xx
      xx...xx
      xG...Gx
      xx@@@xx
ENDMAP

NAME:   corexii_hall_statues_x
DEPTH:  D, Elf, Crypt, Snake
WEIGHT: 2
TAGS:   extra allow_dup serial_corexii_hall
SUBST:  T = T:3 V:3 G:1 .:3
MAP
      xx@@@xx
      xG...Gx
      xx...xx
      xx...xx
      xx...xx
      xG...Gx
xxxxxxxx...xxxxxxxx
xGxxxGxx...xxGxxxGx
@.................@
@........T........@
@.................@
xGxxxGxx...xxGxxxGx
xxxxxxxx...xxxxxxxx
      xG...Gx
      xx...xx
      xx...xx
      xx...xx
      xG...Gx
      xx@@@xx
ENDMAP
